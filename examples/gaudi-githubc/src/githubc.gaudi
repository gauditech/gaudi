// database {
//     engine pg
//     uri 'postgresql://postgres@localhost:5342'
// } 

// models

model User {
    field name { type text }
    relation org_memberships { from OrgMembership, through user }
}

model Org {
    field name { type text }
    field slug { type text, unique }
    field description { type text }
    field optOut { type text, nullable }

    relation repos { from Repo, through org }
    relation memberships { from OrgMembership, through org }

    query members { from memberships.user }
    query public_repos { from repos, filter { is_public is true } }
    query public_issues { from public_repos.issues }
}

model OrgMembership {
    reference user { to User }
    reference org { to Org }
}

model Repo {
    reference org { to Org }

    field name { type text }
    field slug { type text, unique }
    field description { type text }
    field is_public { type boolean, default true }

    relation issues { from Issue, through repo }
}

model Issue {
    field title { type text }
    field body { type text }
    reference repo { to Repo }
}

// entrypoints

entrypoint Orgs {
    target model Org
    identify with slug
    response { name, slug, description, repos { issues { repo { org } } }, members, public_repos, public_issues }

    get endpoint {}
    list endpoint {}

    entrypoint Repos {
        target relation repos
        response { id, slug, description, org_id, issues { repo } }

        get endpoint {}
        list endpoint {}
        create endpoint {}
    }

    entrypoint PublicRepos {
        target relation public_repos
        response { id, slug, description, org_id }

        get endpoint {}
        list endpoint {}
        create endpoint {}
    }

    entrypoint AllIssues {
        target relation public_issues
        response { id, title, body }
        list endpoint {}
    }

    entrypoint Members {
        target relation members
        response { id, name }
        list endpoint {}
        get endpoint {}
    }
}