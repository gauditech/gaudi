// database {
//     engine pg
//     uri 'postgresql://postgres@localhost:5342'
// }

runtime HooksRuntime {
    sourcePath "hooks"
}

generate client {
  target js,
  api entrypoint
  output "src/web/js "
}

// ----- models

model Org {
    field name {
        type text
        validate {
            hook {
                default arg name
                source noUnicode from "githubc.js"
            }
        }
    }
    field slug {
        type text,
        unique,
        validate {
            hook noSpace { // hook can be named for documentation. Maybe use the name for hook code?
                default arg slug
                inline `!/\s/.test(slug)`
            }
        }
    }
    field description { type text }
    field optOut { type text, nullable }

    relation repos { from Repo, through org }
    relation memberships { from OrgMembership, through org }

    query members { from memberships.user }
    query public_repos { from repos, filter { is_public is true } }
    query public_issues { from public_repos.issues }

    hook nameAndDesc {
        arg test query {
            select { name, description }
        }
        inline `test.name + ": " + test.description`
    }
}

model OrgMembership {
    reference user { to User }
    reference org { to Org }
}

model Repo {
    reference org { to Org }

    field name { type text }
    field slug { type text, unique }
    field description { type text }
    field is_public { type boolean, default true }

    relation issues { from Issue, through repo }
}

model Issue {
    field title { type text }
    field body { type text }
    reference repo { to Repo }
}

// ----- start auth model
model User {
    field name { type text }
    relation authLocal { from UserAuthLocal, through user }
    relation accessToken { from UserAccessToken, through user }
    relation org_memberships { from OrgMembership, through user }
}
model UserAuthLocal {
    reference user { to User }
    field username { type text, unique }
    field password { type text }
}
model UserAccessToken {
    reference user { to User }
    field token { type text, unique }
    field expiryDate { type text }
}
// ----- end auth model

// ----- entrypoints

entrypoint Orgs {
    target model Org as org
    identify with slug
    response { name, slug, description, repos { issues { title, body, repo { org } } }, members, public_repos, public_issues, nameAndDesc }

    get endpoint {}
    list endpoint {}
    create endpoint {}
    update endpoint {}

    entrypoint Repos {
        target relation repos
        response { id, slug, description, org_id, issues { repo } }

        get endpoint {}
        list endpoint {}
        create endpoint {}
        update endpoint {}
        delete endpoint {}
    }

    entrypoint PublicRepos {
        target relation public_repos
        response { id, slug, description, org_id }

        get endpoint {}
        list endpoint {}
        // create endpoint {}
    }

    entrypoint AllIssues {
        target relation public_issues
        response { id, title, body }
        list endpoint {}
    }

    entrypoint Members {
        target relation members
        response { id, name }
        list endpoint {}
        get endpoint {}
    }
}

// ----- populators

populator Dev {
  // org without repos and users
  populate EmptyOrg {
    target model Org as org

    // org
    set name "Organization 1"
    set slug "org-1"
    set description "Description of org-1"
    set optOut null
  }

  // org without repos and users
  populate OrgWith1RepoAnd1Member {
    target model Org as org

    // org
    set name "Organization 2"
    set slug "org-2"
    set description hook {
        arg slug slug
        inline `"Description of " + slug`
    }
    set optOut hook {
        arg slug slug
        inline `"Opt out from org " + slug`
    }

    populate Repos {
        target relation repos as repo

        // fields
        set name concat("Repo 1 at ", org.slug)
        set slug hook {
            arg orgSlug org.slug
            inline `"repo-1-" + orgSlug`
        }
        set description hook {
            arg slug slug
            inline `"Description for " + slug`
        }
        set is_public true
    }
  }

  // org without repos and users
  populate OrgWithMultipleReposAndMembers {
    target model Org as org

    repeat { start 3, end 5 } as iter

    // org
    set name hook {
        arg iter iter
        inline `"Organization " + iter.current`
    }
    set slug hook {
        arg iter iter
        inline `"org-" + iter.current`
    }
    set description hook {
        arg slug slug
        inline `"Description of " + slug`
    }
    set optOut hook {
        arg slug slug
        inline `"Opt out from org " + slug`
    }

    populate Repos {
        target relation repos as repo

        repeat { start 1, end 3 } as riter

        // fields
        set name hook {
            arg riter riter
            arg orgSlug org.slug
            inline `"Repo " + riter.current + " at " + orgSlug`
        }
        set slug hook {
            arg riter riter
            arg orgSlug org.slug
            inline `"repo-" + riter.current + "-" + orgSlug`
        }
        set description hook {
            arg slug slug
            inline `"Description for " + slug`
        }
        set is_public true
    }
  }
}
