
auth {
  method basic {}
}

runtime TestRuntime {
  source path "./src/e2e/api/hooks"
}

model Box {
  field name { type string, unique }
  field is_public { type boolean }
  reference owner { to AuthUser }
  relation items { from Item, through box }
}

model Item {
  field name { type string }
  field is_public { type boolean }
  reference box { to Box }
}

api {
  entrypoint Box as box {
    identify { through name }

    list endpoint {
      // user must be logged in
      authorize { @auth.id is not null }
    }
    get endpoint {
      authorize { box.is_public or @auth.id is box.owner.id }
    }
    create endpoint {
      authorize { @auth.id is not null }
      action {
        create {
          set owner_id @auth.id
        }
      }
    }

    custom endpoint {
      path "fetchAuthToken"
      method POST
      cardinality many

      action {
        execute {
          responds

          hook {
            arg token @requestAuthToken

            source customActionResponds from "hooks.js"
          }
        }
      }
    }

    entrypoint items as item {
      identify { through name }

      authorize { box.is_public or @auth.id is box.owner.id }

      get endpoint {
        authorize { item.is_public }
      }
    }
  }
}
