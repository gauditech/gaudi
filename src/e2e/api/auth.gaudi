auth {
  method basic {
    onRegisterAction {
      create UserProfile as userProfile {
        set user @auth
      }
    }
  }
}

model UserProfile {
  field displayName { type text }
  reference user { to @auth }
}

model Box {
  field name { type text, unique }
  field is_public { type boolean }
  reference owner { to @auth }
  relation items { from Item, through box }
}

model Item {
  field name { type text }
  field is_public { type boolean }
  reference box { to Box }
}

entrypoint Boxes {
  target model Box as box
  identify with name

  list endpoint {
    // user must be logged in
    authorize { @auth.id }
  }
  get endpoint {
    authorize { box.is_public or @auth.id is box.owner.id }
  }
  create endpoint {
    authorize { @auth.id }
    action {
      create {
        set owner_id @auth.id
      }
    }
  }

  entrypoint Items {
    target relation items as item
    identify with name

    authorize { box.is_public or @auth.id is box.owner.id }

    get endpoint {
      authorize { item.is_public }
    }
  }
}

entrypoint UserProfiles {
  target model UserProfile
  identify with user_id

  get endpoint {}
}
