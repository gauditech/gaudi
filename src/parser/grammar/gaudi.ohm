Gaudi {
  Definition
    = NewlineBody<Model, ";">

  // Override Ohm's built-in definition of space.
  space += comment

  Model
    = "model" identifier "{" NewlineBody<ModelBody, ";"> "}"

  ModelBody
    = Field
    | Reference
    | Relation
    | Query

  Field
    = "field" identifier "{" NonemptyNewlineBody<FieldBody, ","> "}"

  FieldBody
    = "type" identifier -- type
    | "default" literal -- default // we allow only literals for now
    | ("nullable" | "unique") -- tag

  Reference
    = "reference" identifier "{" NonemptyNewlineBody<ReferenceBody, ","> "}"

  ReferenceBody
    = "to" identifier -- to
    | ("nullable" | "unique") -- tag

  Relation
    = "relation" identifier "{" NonemptyNewlineBody<RelationBody, ","> "}"

  RelationBody
    = "from" identifier -- from
    | "through" identifier -- through

  Query
    = "query" identifier "{" NonemptyNewlineBody<QueryBody, ","> "}"

  QueryBody
    = "from" identifier -- from
    | "filter" QueryExp -- filter

  QueryExp = OrExp

  OrExp
    = OrExp "or" AndExp -- or
    | AndExp

  AndExp
    = AndExp "and" EqExp -- and
    | EqExp

  EqExp
    = AndExp "==" CompExp -- eq
    | AndExp "!=" CompExp -- neq
    | CompExp

  CompExp
    = CompExp "<" PrimaryExp -- lt
    | CompExp "<=" PrimaryExp -- lteq
    | CompExp ">" PrimaryExp -- gt
    | CompExp ">=" PrimaryExp -- gteq
    | PrimaryExp

  PrimaryExp
    = "(" QueryExp ")" -- paren
    | "!" PrimaryExp -- not
    | literal -- literal
    | identifier -- identifier

  literal (an literal) = null | boolean | integer | float | string

  null = "null"
  boolean = "true" | "false"
  integer = digit+
  float = digit* "." digit*
  string = "\"" (~"\"" any)* "\""

  identifier (an identifier) = identifierStart identifierPart*
  identifierStart = letter | "_"
  identifierPart = identifierStart | alnum

  NewlineBody<Exp, delimiter>
    = NonemptyNewlineBody<Exp, delimiter>
    | -- empty

  NonemptyNewlineBody<Exp, delimiter>
    = Exp (#delimiterOrNL<delimiter> Exp)* #delimiterOrNL<delimiter>?

  lineTerminator (a new line) = "\n" | "\r" | "\u2028" | "\u2029"

  delimiterOrNL<delimiter>
     = (~lineTerminator space)* (lineTerminator | delimiter)

  comment = multiLineComment | singleLineComment

  multiLineComment = "/*" (~"*/" any)* "*/"
  singleLineComment = "//" (~lineTerminator any)*
}
